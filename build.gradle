/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.2/userguide/java_library_plugin.html
 */
plugins {
    id 'org.springframework.boot' version '2.6.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "com.palantir.docker" version "0.32.0"
    id 'java'
}

apply from: file('gradle/k8shelper.gradle')

version = '0.0.1'

ext {
    fabric8MockVersion = "5.12.2"
    fabric8Version = "5.12.2"
    junitVersion = '4.13.2'
    lombokVersion = '1.18.24'
    minioVersion = '8.3.8'
    okhttp3Verdsion = "4.9.3"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "io.fabric8:kubernetes-client:${fabric8Version}"
    implementation "io.minio:minio:${minioVersion}"
    implementation "com.squareup.okhttp3:okhttp:${okhttp3Verdsion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    implementation "javax.inject:javax.inject:1"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
    testImplementation "junit:junit:${junitVersion}"
    implementation "io.fabric8:kubernetes-server-mock:${fabric8MockVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.3'
}

bootRun {
    dependsOn bootJar
    sourceResources sourceSets.main
    systemProperty 'spring.profiles.active', System.getProperty('spring.profiles.active') ?: 'local'
    systemProperty 'user.timezone', System.getProperty('user.timezone') ?: 'UTC'
}

static def helmExec(List<String> args) {
    def command = ["helm"]
    command.addAll(args)
    def process = new ProcessBuilder(command as String[]).inheritIO().start()
    process.waitFor()
}

task helmInstall() {
    description = "Installs project helm charts"
    doLast {
        helmExec(["install", "mstc-cloud-worker", "${project.projectDir.path}/charts/mstc-cloud-worker", "-n", "mstc-dev"])
    }
}

task helmUninstall(dependsOn: portForwardStop) {
    description = "Uninstalls project helm charts"
    doLast {
        helmExec(["uninstall", "mstc-cloud-worker", "-n", "mstc-dev"])
    }
}

portForward {
    names["minio"] = [9000, 9001]
    names["mstc-work-queue"] = [5672, 15672]
    names["mstc-cloud-worker"] = 8080
    namespace = "mstc-dev"
}
//portForward.dependsOn helmInstall

clean.dependsOn(helmUninstall)

test {
    dependsOn helmInstall, portForward
    finalizedBy helmUninstall

    environment "MINIO_SERVICE_HOST", "localhost"
    environment "MINIO_SERVICE_PORT", "9000"
    environment "WORK_QUEUE_HOST", "localhost"
    environment "WORK_QUEUE_PORT", "5672"
    environment "WORK_QUEUE_USER", "guest"
    environment "WORK_QUEUE_PASS", "guest"

    systemProperty "test.data.dir", "${project.projectDir.path}/src/test/data"
    systemProperty "test.download.dir", "${project.buildDir.path}/download"
    systemProperty "test.resources.dir", "${project.projectDir.path}/src/test/resources"
}

task list(type: Exec) {
    description = "Runs kubectl get all"
    commandLine = ["kubectl", "-n", "mstc-dev", "get", "all"]
}

task makePythonDist(type: Exec) {
    description = "Create Docker image for Python test client"
    commandLine = ["make", "dist"]
    workingDir new File(project.projectDir, "src/test/python/mstc-cloud-worker")
}

docker {
    dependsOn(makePythonDist)
    name "mstc/${project.name}:${project.version}"
    files bootJar.archiveFile
    buildArgs(['JAR_FILE': "${bootJar.archiveFileName.get()}"])
}

task pytest(type: Exec) {
    //dependsOn makePythonDist, helmInstall, portForward
    description = "Runs pytest for the Python test client"
    commandLine = ["poetry", "run", "pytest"]
    workingDir new File(project.projectDir, "src/test/python/mstc-cloud-worker")
    //finalizedBy helmUninstall
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11
